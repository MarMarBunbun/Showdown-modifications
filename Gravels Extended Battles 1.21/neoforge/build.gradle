plugins {
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm' version "2.1.10"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modImplementation "com.cobblemon:neoforge:${cobblemon_file}"
    modImplementation "maven.modrinth:midnightlib:uGySRC8x"
    modImplementation "curse.maven:supermartijn642s-config-lib-438332:5546996"
    forgeRuntimeLibrary("thedarkcolour:kotlinforforge-neoforge:${kotlin_neo_version}") {
        exclude group: "net.neoforged.fancymodloader", module: "loader"
    }
    modImplementation("maven.modrinth:cobblemon-mega-showdown:${megashowdown_neoforge}")

    modImplementation "io.wispforest:accessories-neoforge:${project.accessories_version}"
    forgeRuntimeLibrary("blue.endless:jankson:1.2.2")
    forgeRuntimeLibrary("io.wispforest:endec:0.1.5.1")
    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:jankson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.2")
}
def replaceProperties = [mod_id          : mod_id,
                         mod_name        : mod_name,
                         mod_version     : mod_version,
                         mod_authors     : mod_authors,
                         mod_description : mod_description,
                         mod_base_package: mod_base_package,
                         minecraft_version: minecraft_version,
                         fabric_version  : fabric_api_version,
                         fabric_loader_version  : fabric_loader_version]

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
