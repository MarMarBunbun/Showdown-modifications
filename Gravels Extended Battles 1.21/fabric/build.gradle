plugins {
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm' version "2.1.10"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=gravels_extended_battles"

            runDir "build/datagen"
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation("com.cobblemon:fabric:${cobblemon_file}") { transitive = false }
    modImplementation "maven.modrinth:midnightlib:gDxRS3a2"
    modImplementation("net.fabricmc:fabric-language-kotlin:${fabric_kotlin}")

    //Mega showdown compat
    modImplementation files("libs/mega_showdown-fabric-${megashowdown}.jar")
//    modImplementation("maven.modrinth:cobblemon-mega-showdown:${megashowdown}-fabric")
    modImplementation "curse.maven:supermartijn642s-config-lib-438332:5546988"
    modImplementation("maven.modrinth:supermartijn642s-config-lib:1.1.8-fabric-mc1.21")

    modImplementation include("io.wispforest:accessories-fabric:${project.accessories_version}")
}
def replaceProperties = [mod_id          : mod_id,
                         mod_name        : mod_name,
                         mod_version     : mod_version,
                         mod_authors     : mod_authors,
                         mod_description : mod_description,
                         mod_base_package: mod_base_package,
                         minecraft_version: minecraft_version,
                         fabric_version  : fabric_api_version,
                         fabric_loader_version  : fabric_loader_version]

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
